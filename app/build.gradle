apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'checkstyle'

checkstyle {
    showViolations true
}

def openvpn3SwigFiles = new File(buildDir, "generated/source/ovpn3swig/ovpn3")

task("generateOpenVPN3Swig", type: Exec) {
    doFirst {
        mkdir openvpn3SwigFiles
    }
    commandLine "swig", "-outdir", openvpn3SwigFiles, "-outcurrentdir", "-c++", "-java", "-package", "net.qlink.ovpn3",
            "-Isrc/main/cpp/openvpn3/client", "-Isrc/main/cpp/openvpn3/",
            "-o", "${openvpn3SwigFiles}/ovpncli_wrap.cxx", "-oh", "${openvpn3SwigFiles}/ovpncli_wrap.h",
            "src/main/cpp/openvpn3/javacli/ovpncli.i"

}

android {

    compileSdkVersion 27
    defaultConfig {
        applicationId "com.stratagile.winq.dapp"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 55
        multiDexEnabled true
        versionName "1.2.8 release"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_TOOLCHAIN=clang',
                        '-DANDROID_STL=c++_static'
//                abiFilters 'x86', 'x86_64', 'armeabi', 'armeabi-v7a', 'arm64-v8a'
            }
        }
        ndk {
            abiFilters 'x86', 'armeabi-v7a', 'arm64-v8a'
        }
    }
    // 取消掉系统对.9图片的检查
    aaptOptions.cruncherEnabled = false
    aaptOptions.useNewCruncher = false

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
            minifyEnabled false
            buildConfigField "boolean", "LOG_DEBUG", "true"
            jniDebuggable true
        }
        release {
            minifyEnabled false
//            signingConfig signingConfigs.release
            buildConfigField "boolean", "LOG_DEBUG", "false"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    aaptOptions {
        cruncherEnabled = false
    }
    configurations {
        all*.exclude group: 'com.android.support', module: 'support-v13'
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['src/main/jniLibs']
            assets.srcDirs = ['src/main/assets', 'build/ovpnassets']
        }
        normal {
            java.srcDirs = ["src/ovpn3/java/", openvpn3SwigFiles]
        }

        noovpn3 {
        }

        debug {

        }

        release {

        }
    }

    greendao {
        schemaVersion 41//数据库版本号
        daoPackage 'com.stratagile.qlink.db'//设置DaoMaster、DaoSession、Dao包名
        targetGenDir 'src/main/java'//设置DaoMaster、DaoSession、Dao目录
        //targetGenDirTest：设置生成单元测试目录
        //generateTests：设置自动生成单元测试用例
    }

    flavorDimensions "implementation"

    productFlavors {
        noovpn3 {
            dimension "implementation"
            buildConfigField 'boolean', 'openvpn3', 'false'
        }

        normal {
            dimension "implementation"
            buildConfigField 'boolean', 'openvpn3', 'true'

        }
    }

}

repositories {
    mavenCentral()
    flatDir {
        dirs 'libs' //就是你放aar的目录地址
    }
}

if (project.hasProperty('keystoreFile') &&
        project.hasProperty('keystorePassword') &&
        project.hasProperty('keystoreAliasPassword')) {
    android.signingConfigs.release.storeFile = file(keystoreFile)
    android.signingConfigs.release.storePassword = keystorePassword
    android.signingConfigs.release.keyPassword = keystoreAliasPassword
    android.signingConfigs.release.keyAlias = keystoreAlias
} else {
    android.buildTypes.release.signingConfig = null
}

// Ensure native build is run before assets, so assets are ready to be merged into the apk
android.applicationVariants.all { variant ->
    variant.mergeAssets.dependsOn(variant.externalNativeBuildTasks)
}


android.applicationVariants.all { variant ->
    variant.productFlavors.each {
        if (it.dimension == 'implementation' && it.name != 'noovpn3')
            variant.getJavaCompiler().dependsOn(generateOpenVPN3Swig)

    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-dynamic-animation:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    testImplementation 'junit:junit:4.12'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:multidex:1.0.3'
    // Net
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    //    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.3.0'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'
    // Annotation
    implementation 'com.google.dagger:dagger:2.16'
    kapt 'com.google.dagger:dagger-compiler:2.16'
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'
    // Rx
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.12'
    // Bus
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.github.zhaokaiqiang.klog:library:1.6.0'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.40'
    //图片加载框架
    implementation 'com.github.bumptech.glide:glide:4.7.1'
    kapt 'com.github.bumptech.glide:compiler:4.7.1'
    //butterknife
    implementation 'com.jakewharton:butterknife:8.8.1'
    kapt 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'com.yanzhenjie:permission:1.1.2'
    implementation files('libs/org.apache.http.legacy.jar')
    implementation files('libs/json-lib-2.4-jdk15.jar')
    implementation files('libs/wowoviewpager.aar')
    implementation 'com.alibaba:fastjson:1.2.37'
    implementation 'org.greenrobot:greendao:3.2.2'
    implementation 'com.lzy.widget:view-core:0.1.5'
    implementation 'com.tencent.bugly:crashreport:2.6.6.1'
    implementation 'com.tencent.bugly:nativecrashreport:3.3.1'
    implementation project(':RxTools-library')
    implementation project(':library')
    implementation 'net.zetetic:android-database-sqlcipher:3.5.7@aar'
    implementation 'com.kyleduo.switchbutton:library:2.0.0'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.2'
    /* implementation 'com.google.android.gms:play-services-maps:15.0.0'*/
    implementation 'cc.solart:turbo-recyclerview-helper:1.0.3-beta'
    implementation 'com.github.yuweiguocn:GreenDaoUpgradeHelper:v2.0.2'
    //谷歌的kotlin扩展库
    implementation 'androidx.core:core-ktx:0.3'
    implementation ('com.google.guava:guava:23.3-android')
            {
                exclude group: 'com.google.code.findbugs'
            }
    implementation project(':neoutils')
    implementation 'com.github.kotlin-graphics:kotlin-unsigned:v2.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation ('com.google.code.gson:gson:2.8.2')
    implementation ('com.github.salomonbrys.kotson:kotson:2.5.0')
    implementation ('com.google.firebase:firebase-core:15.0.0')
    implementation ('org.web3j:core:3.3.1-android')
    implementation ('com.facebook.android:facebook-android-sdk:[4,5)'){
        exclude group: 'com.google.zxing'
    }

    implementation "android.arch.lifecycle:runtime:$arch_version"
    implementation "android.arch.lifecycle:common-java8:$arch_version"
    kapt "android.arch.lifecycle:compiler:$arch_version"
    implementation "android.arch.lifecycle:extensions:$arch_version"

//    debugCompile   'com.squareup.leakcanary:leakcanary-android:1.5.4'
//    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
//    testCompile    'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

}
// ADD THIS AT THE BOTTOM
apply plugin: 'com.google.gms.google-services'